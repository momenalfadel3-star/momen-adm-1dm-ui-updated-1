plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dev.flutter.flutter-gradle-plugin"
}

android {
    namespace "com.gopeed.gopeed"
    compileSdk 34
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    defaultConfig {
        applicationId "com.gopeed.gopeed"
        minSdk 21
        targetSdk 34
        versionCode flutter.versionCode
        versionName flutter.versionName
    }

    signingConfigs {
        release {
            keyAlias "upload"
            keyPassword System.getenv("APK_KEY_PASSWORD")
            storeFile file("upload-keystore.jks")
            storePassword System.getenv("APK_STORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"

            if (System.getenv("APK_KEY_PASSWORD") != null && System.getenv("APK_STORE_PASSWORD") != null) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
    }

    repositories {
        flatDir {
            dirs "libs"
        }
    }
}

flutter {
    source "../.."
}

dependencies {
    implementation(name: "libgopeed", ext: "aar")
}
